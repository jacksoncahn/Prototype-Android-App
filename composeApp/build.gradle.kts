import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.util.Properties

val localProperties = Properties()
val localPropertiesFile = project.rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.inputStream())
}

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.kotlinxSerialization)
}

kotlin {
    androidTarget {
        @OptIn(ExperimentalKotlinGradlePluginApi::class)
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }

    sourceSets {
        androidMain.dependencies {
            implementation(libs.androidx.compose.ui.tooling.preview)
            implementation(libs.androidx.activity.compose)
            implementation(libs.androidx.viewmodel.compose)
            implementation(libs.androidx.navigation.compose)
            implementation(libs.koin.androidx.compose)
            implementation(libs.coil.compose)
            implementation(libs.coil.network.ktor)
            implementation(libs.maps.compose)
            implementation(libs.play.services.maps)
            implementation("androidx.compose.material:material-icons-extended-android:1.7.8")
        }
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)
            implementation(projects.shared)
            implementation(libs.kotlinx.serialization.core)

            //REST API
            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.serialization.kotlinx.json)
        }
    }
}

android {
    namespace = "com.jetbrains.kmpapp"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.jetbrains.kmpapp"
        minSdk = 24
        targetSdk = 35
        versionCode = 1
        versionName = "1.0"
        manifestPlaceholders["MAPS_API_KEY"] = localProperties.getProperty("MAPS_API_KEY", "")

        //Compose Multiplatform / Kotlin Multiplatform plugin disables buildConfig by default, so we have to enable it here
        buildFeatures {
            buildConfig = true
        }

        // Must wrap the value in escaped quotes ("${...}") because BuildConfig generates Java/Kotlin code.
        // Without the quotes, the compiler sees an unquoted identifier instead of a string literal,
        // which causes "cannot find symbol" errors.

        buildConfigField(
            "String",
            "WORKOS_CLIENT_ID",
            "\"${localProperties.getProperty("WORKOS_CLIENT_ID")}\""
        )
//        buildConfigField(
//            "String",
//            "WORKOS_API_KEY",
//            "\"${localProperties.getProperty("WORKOS_API_KEY")}\""
//        )
//        buildConfigField(
//            "String",
//            "WORKOS_COOKIE_PASSWORD",
//            "\"${localProperties.getProperty("WORKOS_COOKIE_PASSWORD")}\""
//        )

    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    buildTypes {
        getByName("release") {
            isMinifyEnabled = false

        }
    }


    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

dependencies {
    debugImplementation(libs.androidx.compose.ui.tooling)
}
